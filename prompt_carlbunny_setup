#
# A simple theme that displays relevant, contextual information.
#
# Authors:
#   Sorin Ionescu <sorin.ionescu@gmail.com>
#
# Screenshots:
#   http://i.imgur.com/nBEEZ.png
#

return_code="%(?..%F{red}%?)"
PROMPT_BASE='[%F{118}%n@%m%f %F{33}${_prompt_sorin_pwd}%f%F{yellow}${scm_info_string}%(!.%B%F{red}#%f%b.)${editor_info[keymap]}] '
#RPROMPT_BASE="${return_code} %F{blue}[%F{yellow}%D{%H:%M:%S}%F{blue}]%f"
RPROMPT_BASE="%F{blue}[%F{yellow}%D{%H:%M:%S}%F{blue}]%f"
SPROMPT_BASE='zsh: correct %F{red}%R%f to %F{green}%r%f [nyae]? '

# Load dependencies.
pmodload 'helper'

function prompt_sorin_pwd {
  local pwd="${PWD/#$HOME/~}"

  if [[ "$pwd" == (#m)[/~] ]]; then
    _prompt_sorin_pwd="$MATCH"
    unset MATCH
  else
    _prompt_sorin_pwd="${${${(@j:/:M)${(@s:/:)pwd}##.#?}:h}%/}/${pwd:t}"
  fi
}

function prompt_sorin_precmd {
  setopt LOCAL_OPTIONS
  unsetopt XTRACE KSH_ARRAYS

  # Format PWD.
  prompt_sorin_pwd

  # Get Git repository information.
  if (( $+functions[_dotfiles_scm_info] )); then
    scm_info_string=$(_dotfiles_scm_info)
    if [ ! -z "$scm_info_string" ]; then
      scm_info_string="${scm_info_string}"
    fi
  fi
  #if (( $+functions[git-info] )); then
    #git-info
  #fi
}

################################################################################
# Show the exec time
# Note RPROMPT itself can not extract bash variable, so we construct RPROMPT 
# every time
function preexec() {
    timer=${timer:-$SECONDS}
}

function precmd() {
  if [ $timer ]; then
    timer_show=$(($SECONDS - $timer))
    exec_time="%F{cyan}${timer_show}s"
    RPROMPT="${return_code} ${exec_time} ${RPROMPT_BASE}"
    unset timer
  else 
    RPROMPT="${return_code} ${RPROMPT_BASE}"
  fi
}
################################################################################

function prompt_sorin_setup {
  setopt LOCAL_OPTIONS
  unsetopt XTRACE KSH_ARRAYS
  prompt_opts=(cr percent subst)

  # Load required functions.
  autoload -Uz add-zsh-hook

  # Add hook for calling git-info before each command.
  add-zsh-hook precmd prompt_sorin_precmd

  # Set editor-info parameters.
  zstyle ':prezto:module:editor:info:completing' format '%B%F{red}...%f%b'
  zstyle ':prezto:module:editor:info:keymap:primary' format '%B%F{blue}$%f%b'
  zstyle ':prezto:module:editor:info:keymap:primary:overwrite' format '%F{red}♺%f'
  zstyle ':prezto:module:editor:info:keymap:alternate' format '%B%F{green}❮%F{yellow}❮%F{red}❮%f%b'

  # Set git-info parameters.
  zstyle ':prezto:module:git:info' verbose 'yes'
  zstyle ':prezto:module:git:info:action' format ':%%B%F{yellow}%s%f%%b'
  zstyle ':prezto:module:git:info:added' format ' %%B%F{green}✚%f%%b'
  zstyle ':prezto:module:git:info:ahead' format ' %%B%F{yellow}⬆%f%%b'
  zstyle ':prezto:module:git:info:behind' format ' %%B%F{yellow}⬇%f%%b'
  zstyle ':prezto:module:git:info:branch' format ':%F{green}%b%f'
  zstyle ':prezto:module:git:info:commit' format ':%F{green}%.7c%f'
  zstyle ':prezto:module:git:info:deleted' format ' %%B%F{red}✖%f%%b'
  zstyle ':prezto:module:git:info:modified' format ' %%B%F{blue}✱%f%%b'
  zstyle ':prezto:module:git:info:position' format ':%F{red}%p%f'
  zstyle ':prezto:module:git:info:renamed' format ' %%B%F{magenta}➜%f%%b'
  zstyle ':prezto:module:git:info:stashed' format ' %%B%F{cyan}✭%f%%b'
  zstyle ':prezto:module:git:info:unmerged' format ' %%B%F{yellow}═%f%%b'
  zstyle ':prezto:module:git:info:untracked' format ' %%B%F{white}◼%f%%b'
  zstyle ':prezto:module:git:info:keys' format \
    'prompt' ' %F{blue}git%f$(coalesce "%b" "%p" "%c")%s' \
    'rprompt' '%A%B%S%a%d%m%r%U%u'
  
  # 118 : green
  # 33 : blue

  # Define prompts.
  PROMPT=$PROMPT_BASE
  RPROMPT=$RPROMPT_BASE
  SPROMPT=$SPROMPT_BASE
}

prompt_sorin_setup "$@"

